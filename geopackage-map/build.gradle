apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "mil.nga.geopackage.map"
archivesBaseName = "geopackage-android-map"
version = "4.0.1"
def remotePublish = false

android {
    compileSdkVersion 29

    dexOptions {
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true

        testApplicationId 'mil.nga.geopackage.map.test'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    configurations {
        javadocDeps
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }
    }

    task javadoc(type: Javadoc) {
        title = "$archivesBaseName $version API"
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.javadocDeps
        classpath += files("${android.sdkDirectory}/extras/google/google_play_services/libproject/google-play-services_lib/libs/google-play-services.jar")
        options.links "https://ngageoint.github.io/tiff-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-wkb-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-wkt-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-proj-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-geojson-java/docs/api/"
        options.links "https://ngageoint.github.io/ogc-api-features-json-java/docs/api/"
        options.links "https://ngageoint.github.io/geopackage-core-java/docs/api/"
        options.links "https://ngageoint.github.io/geopackage-android/docs/api/"
        options.links "http://ormlite.com/javadoc/ormlite-core/"
        options.links "http://ormlite.com/javadoc/ormlite-android/"
        options.links "https://docs.oracle.com/javase/8/docs/api/"
        options.links "http://hjg.com.ar/pngj/apidocs/"
        options.linksOffline "https://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        options.linksOffline "https://d.android.com/reference","${android.sdkDirectory}/extras/google/google_play_services/reference"
        destinationDir = file("../javadoc/")
        failOnError false
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set("javadoc")
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.java.srcDirs
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required {remotePublish}
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                if (!remotePublish) {

                    repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository')

                } else {

                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'GeoPackage Android Map'
                        packaging 'aar'
                        description 'GeoPackage Android Map implementation'
                        url 'https://github.com/ngageoint/geopackage-android-map'

                        scm {
                            url 'git@github.com:ngageoint/geopackage-android-map.git'
                            connection 'scm:git:git@github.com:ngageoint/geopackage-android-map.git'
                            developerConnection 'scm:git:git@github.com:ngageoint/geopackage-android-map.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'https://github.com/ngageoint/geopackage-android-map/blob/master/LICENSE.txt'
                                distribution 'repo'
                            }
                        }

                        organization {
                            name 'National Geospatial-Intelligence Agency'
                            url 'https://www.nga.mil'
                        }

                        developers {
                            developer {
                                id 'bosborn'
                                name 'Brian Osborn'
                                email 'osbornb@bit-sys.com'
                                organizationUrl 'https://www.bit-sys.com'
                                roles {
                                    role 'developer'
                                }
                                timezone 'UTC-07'
                            }
                        }
                    }
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

}

dependencies {
    api 'com.google.android.gms:play-services-maps:17.0.0'
    api 'com.google.maps.android:android-maps-utils:2.0.3'
    api 'mil.nga.geopackage:geopackage-android:4.0.0'
    //api project(':geopackage-sdk') // uncomment me to build locally
    javadocDeps 'mil.nga.geopackage:geopackage-android:4.0.0'
    testImplementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}
